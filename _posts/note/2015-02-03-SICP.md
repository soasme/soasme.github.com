---
layout: post
category: note
---

# SICP note

## 第三章

#### 练习 3.1

累加器: 反复调用数值参数, 是各个参数累加到一个和数中.
过程 `make-accumulator` 生成的累加器维持一个独立的和.

{% highlight scheme %}
(define (make-accumulator sum)
  (define (add adder)
    (begin (set! sum (+ sum adder))
           sum))
  add)
{% endhighlight %}

{% highlight scheme %}
1 ]=> (define A (make-accumulator 5))

;Value: a

1 ]=> (A 10)

;Value: 15

1 ]=> (A 10)

;Value: 25
{% endhighlight %}

#### 练习 3.2

过程 `make-monitored` 统计给定过程的调用次数.
给定过程 `f` 为输入, 它有一个参数.

{% highlight scheme %}
(define (make-monitored f)
  (let ((count 0))
    (define (how-many-calls?)
      count)
    (define (monitor param)
      (begin (set! count (+ count 1))
             (f param)))
    (define (reset-count)
      (begin (set! count 0)
             0))
    (define (dispatch symbol)
      (cond ((eq? symbol 'how-many-calls?) (how-many-calls?))
            ((eq? symbol 'reset-count) (reset-count))
            (else (monitor symbol))))
    dispatch))

{% endhighlight %}

{% highlight scheme %}
1 ]=> (define s (make-monitored sqrt))

;Value: s

1 ]=> (s 100)

;Value: 10

1 ]=> (s 400)

;Value: 20

1 ]=> (s 'how-many-calls?)

;Value: 2

1 ]=> (s 'reset-count)

;Value: 0

1 ]=> (s 'how-many-calls?)

;Value: 0
{% endhighlight %}

#### 练习 3.3

过程 `make-account` 带密码保护, 账户对象收到的消息需要同时提供账户创建是给定的密码.

{% highlight scheme %}
(define (make-account balance password)
  (define (withdraw amount)
    (if (>= balance amount)
        (begin (set! balance (- balance amount))
               balance)
        "Insufficient funds."))
  (define (deposit amount)
    (begin (set! balance (+ balance amount))
           balance))
  (define (dispatch input-password method)
    (if (eq? password input-password)
        (cond ((eq? method 'withdraw) withdraw)
              ((eq? method 'deposit) deposit)
              (else (error "Unknown request -- MAKE-ACCOUNT" method)))
        (error "Incorrent password" method)))
  dispatch)
{% endhighlight %}

{% highlight scheme %}
1 ]=> (define acc (make-account 100 'secret-password))

;Value: acc

1 ]=> ((acc 'secret-password 'withdraw) 50)

;Value: 50

1 ]=> ((acc 'wrong-password 'withdraw) 50)

;Incorrent password withdraw
;To continue, call RESTART with an option number:
; (RESTART 1) => Return to read-eval-print level 1.
{% endhighlight %}

#### 练习 3.4

用不正确的密码访问7次, 调用过程 `call-the-cops`.

{% highlight scheme %}
(define (make-account balance password)
  (let ((wrong-times 0))
    (define (withdraw amount)
      (if (>= balance amount)
          (begin (set! balance (- balance amount))
                 balance)
          "Insufficient funds."))
    (define (deposit amount)
      (begin (set! balance (+ balance amount))
             balance))
    (define (call-the-cops)
      (display "Cops are on the way."))
    (define (verify-password user-password input)
      (if (eq? user-password input)
          (begin (set! wrong-times 0)
                 #t)
          (begin (set! wrong-times (+ wrong-times 1))
                 (if (> wrong-times 7)
                     (call-the-cops))
                 #f)))
    (define (dispatch input-password method)
      (if (verify-password password input-password)
          (cond ((eq? method 'withdraw) withdraw)
                ((eq? method 'deposit) deposit)
                (else (error "Unknown request -- MAKE-ACCOUNT" method)))
          (lambda (x) (display "Incorrent password"))))
    dispatch))
{% endhighlight %}

{% highlight scheme %}
1 ]=> (define acc (make-account 100 'secret-password))

;Value: acc

1 ]=> ((acc 'incorrect-password 'withdraw) 100)
Incorrent password
;Unspecified return value

1 ]=> ((acc 'incorrect-password 'withdraw) 100)
Incorrent password
;Unspecified return value

1 ]=> ((acc 'incorrect-password 'withdraw) 100)
Incorrent password
;Unspecified return value

1 ]=> ((acc 'incorrect-password 'withdraw) 100)
Incorrent password
;Unspecified return value

1 ]=> ((acc 'incorrect-password 'withdraw) 100)
Incorrent password
;Unspecified return value

1 ]=> ((acc 'incorrect-password 'withdraw) 100)
Incorrent password
;Unspecified return value

1 ]=> ((acc 'incorrect-password 'withdraw) 100)
Incorrent password
;Unspecified return value

1 ]=> ((acc 'incorrect-password 'withdraw) 100)
Cops are on the way.Incorrent password
;Unspecified return value
{% endhighlight %}
